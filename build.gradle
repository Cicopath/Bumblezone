buildscript {
	repositories {
		maven { url "https://plugins.gradle.org/m2/" }
	}
}

plugins {
	alias(libs.plugins.quilt.loom)
	id 'maven-publish'
	id "net.darkhax.curseforgegradle" version "1.0.7"
	id "com.modrinth.minotaur" version "2.+"
}

archivesBaseName = "${project.modid}_quilt"
version = "${project.mod_version}+${libs.versions.minecraft.get()}"
group = project.maven_group

loom {
	accessWidenerPath = file("src/main/resources/the_bumblezone.accesswidener")
}

configurations.all {
	resolutionStrategy.dependencySubstitution {
		substitute module('net.fabricmc:fabric-loader') using module(libs.quilt.loader.get().toString())
		all { DependencySubstitution dependency ->
			if (dependency.requested instanceof ModuleComponentSelector && dependency.requested.group == 'net.fabricmc.fabric-api') {
				if (dependency.requested.module == 'fabric-api')
					dependency.useTarget libs.quilted.fabric.api.get().toString()
				else
					dependency.useTarget 'org.quiltmc.quilted-fabric-api' + ':' + dependency.requested.module + ':' + libs.versions.quilted.fabric.api.get()
			}
		}
	}
}

repositories {
	maven {
		url = 'https://maven.shedaniel.me'
		content {
			includeGroup 'me.shedaniel.cloth'
			includeGroup 'me.shedaniel'
		}
	}
	maven {
		url = 'https://www.cursemaven.com'
		content {
			includeGroup 'curse.maven'
		}
	}
	maven {
		url = 'https://api.modrinth.com/maven/'
		content {
			includeGroup 'maven.modrinth'
		}
	}
	maven {
		url = 'https://maven.cafeteria.dev/releases'
		content {
			includeGroup 'dev.cafeteria'
		}
	}
	maven {
		name = 'Ladysnake Mods'
		url = 'https://ladysnake.jfrog.io/artifactory/mods'
		content {
			includeGroup 'io.github.ladysnake'
			includeGroupByRegex 'dev\\.onyxstudios.*'
			includeGroupByRegex 'dev\\.emi.*'
		}
	}
	maven {
		name = 'Nexus OSS Repository'
		url = 'https://oss.sonatype.org/content/repositories/snapshots'
		content {
			includeGroup 'me.lucko'
		}
	}
	maven {
		url = 'https://nexus.resourcefulbees.com/repository/maven-public/'
		content {
			includeGroup 'com.telepathicgrunt'
		}
	}
	maven {
		url = 'https://maven.blamejared.com'
		content {
			includeGroup 'mezz.jei'
		}
	}
	maven {
		// location of a maven mirror for JEI files, as a fallback
		name = "ModMaven"
		url = "https://modmaven.dev"
	}
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/"
		content {
			includeGroupByRegex 'dev\\.emi.*'
		}
	}
	maven {
		url "https://maven.jamieswhiteshirt.com/libs-release"
		content {
			includeGroup "com.jamieswhiteshirt"
		}
	}
	maven {
		url = 'https://maven.architectury.dev/'
		content {
			includeGroup 'dev.architectury'
		}
	}
	maven {
		url 'https://jitpack.io'
		content {
			includeGroup 'com.github.llamalad7.mixinextras'
		}
	}
}

dependencies {
	minecraft libs.minecraft
	mappings( loom.layered {
		"org.quiltmc:quilt-mappings:${libs.versions.quilt.mappings.get()}:intermediary-v2"
		officialMojangMappings()
	})
	modImplementation libs.quilt.loader
	modImplementation libs.quilted.fabric.api

	include(implementation(annotationProcessor("com.github.llamalad7.mixinextras:mixinextras-fabric:${libs.versions.mixinextra.get()}")))

	modImplementation "maven.modrinth:midnightlib:${libs.versions.midnightlib.get()}"
	modRuntimeOnly "maven.modrinth:midnightlib:${libs.versions.midnightlib.get()}"

	modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${libs.versions.cardinal.get()}"
	modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${libs.versions.cardinal.get()}"
	include "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${libs.versions.cardinal.get()}"
	include "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${libs.versions.cardinal.get()}"

	modImplementation "dev.cafeteria:fake-player-api:${libs.versions.fake.player.get()}"
	include "dev.cafeteria:fake-player-api:${libs.versions.fake.player.get()}"

	modCompileOnly "maven.modrinth:modmenu:${libs.versions.mod.menu.get()}"
	modRuntimeOnly "maven.modrinth:modmenu:${libs.versions.mod.menu.get()}"

	modCompileOnly "dev.emi:emi:${libs.versions.emi.get()}"
	//modRuntimeOnly "dev.emi:emi:${libs.versions.emi.get()}"

	modCompileOnly "me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:${libs.versions.rei.get()}"
	modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:${libs.versions.rei.get()}"

	modCompileOnlyApi "curse.maven:jei-238222:3944625"
	//modCompileOnlyApi "mezz.jei:jei-${libs.versions.jeimc.get()}-fabric:${libs.versions.jei.get()}"

	//modRuntimeOnly "com.jamieswhiteshirt:reach-entity-attributes:2.3.0"
	//modRuntimeOnly "curse.maven:travelersbackpacks-541171:4451504"
	//modRuntimeOnly "me.shedaniel.cloth:cloth-config-fabric:${libs.versions.cloth.config.get()}"
	//modRuntimeOnly "dev.architectury:architectury-fabric:${libs.versions.architectury.get()}"
	//modRuntimeOnly "dev.emi:trinkets:${libs.versions.trinkets.get()}"
	modCompileOnly "dev.emi:trinkets:${libs.versions.trinkets.get()}"

	//modCompileOnly "curse.maven:llamarama-449083:3938753"
	//modRuntimeOnly "curse.maven:llamarama-449083:3938753"

	//modCompileOnly "curse.maven:friends_and_foes-551364:3725313"
	//modCompileOnly "curse.maven:bee_better-351600:3726160"
	//modRuntimeOnly "curse.maven:architectury-419697:3717905"
	//modRuntimeOnly "curse.maven:bee_better-351600:3726160"

	modRuntimeOnly "com.telepathicgrunt:CommandStructures-Fabric:${libs.versions.commandstructures.get()}"
	modRuntimeOnly "com.telepathicgrunt:StructureVoidToggle-Fabric:${libs.versions.structurevoidtoggle.get()}"

	//modCompileOnly "curse.maven:friends_and_foes-551364:3827720"
	//modCompileOnly "curse.maven:bee_better-351600:3726160"

	modCompileOnly "io.github.ladysnake:requiem-api:${libs.versions.requiem.get()}"
	modCompileOnly "io.github.ladysnake:requiem-core:${libs.versions.requiem.get()}"
	modCompileOnly "io.github.ladysnake:requiem:${libs.versions.requiem.get()}"

	//modImplementation ("curse.maven:mobcatcher-498389:3575752") { transitive = false }
	//modCompileOnly ("curse.maven:charm-318872:3355096") { transitive = false }
}

apply from: 'gradle/processresources.gradle'

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = 'UTF-8'
	it.options.release.set(17)
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${archivesBaseName}"}
	}
}

apply from: 'gradle/maven.gradle'

String changelogText = file("CHANGELOG.md").getText('UTF-8')

task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
	apiToken = "$System.env.CURSEFORGEKEY"
	int projectId = 363949

	def mainFile = upload(projectId, remapJar)
	mainFile.changelog = changelogText
	mainFile.changelogType = "markdown"
	mainFile.releaseType = "release"
	mainFile.displayName = "The Bumblezone v${project.mod_version} (${libs.versions.minecraft.get()} MC Quilt)"
	mainFile.addGameVersion("1.19.2")
	mainFile.addModLoader("Quilt")
	mainFile.addRequirement("midnightlib")
	mainFile.addRequirement("qsl")
	mainFile.addOptional("continuity")
	mainFile.addOptional("unofficial-sodium-biome-blending-fix")
	mainFile.addOptional("indium")
	mainFile.addIncompatibility("lanterns-bow")
}

modrinth {
	token = System.getenv("MODRINTH")
	versionName = "The Bumblezone"
	versionNumber = project.version + "+quilt"
	projectId = "eA8SXqWL"
	uploadFile = remapJar
	changelog = changelogText
	versionType = "RELEASE"
	gameVersions = ["1.19.2"]
	loaders = ["quilt"]
	dependencies {
		required.project "qsl"
		required.project "midnightlib"
		optional.project "continuity"
		optional.project "unofficial-sodium-biome-blending-fix"
		optional.project "indium"
		incompatible.project "lanterns-bow"
	}
}

task buildAndRelease{}
tasks.publish.mustRunAfter tasks.build
tasks.publishCurseForge.mustRunAfter tasks.publish
tasks.modrinth.mustRunAfter tasks.publish
tasks.publishCurseForge.dependsOn("remapJar")
tasks.buildAndRelease.dependsOn(tasks.build, tasks.publish, tasks.publishCurseForge, tasks.modrinth)
