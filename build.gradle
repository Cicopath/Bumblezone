import net.fabricmc.loom.task.RemapJarTask

buildscript {
	repositories {
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
		classpath group: 'gradle.plugin.com.modrinth.minotaur', name: 'Minotaur', version: '1.1.0'
	}
}

plugins {
	id 'fabric-loom' version '0.7-SNAPSHOT'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

version = "${project.minecraft_version}-${project.mod_version}-fabric"
archivesBaseName = project.archives_base_name
group = project.maven_group

minecraft {
	accessWidener = file("src/main/resources/the_bumblezone.accesswidener")
}

repositories {
	maven { url 'https://maven.fabricmc.net/io/github/prospector/modmenu/' }
	maven { url = "https://www.cursemaven.com" }
	maven { url 'https://jitpack.io' }
	maven { url "https://maven.shedaniel.me/" }
	maven {
		name = "Ladysnake Libs"
		url = 'https://dl.bintray.com/ladysnake/libs'
	}
	maven {
		name = "Cotton"
		url = "http://server.bbkr.space:8081/artifactory/libs-release/"
	}
	maven {
		name "Siphalor's Maven"
		url "https://maven.siphalor.de"
	}
	maven {
		url "https://nexus.resourcefulbees.com/repository/maven-public/"
	}
}

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation ("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}") {
		force = true //Force the version we want rather than trusting Gradle to pick it over transitive suggestions
	}

	modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}

	modApi "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:${project.cardinal_version}"
	include "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:${project.cardinal_version}"

	modApi "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:${project.cardinal_version}"
	include "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:${project.cardinal_version}"

	modImplementation "io.github.prospector:modmenu:${project.mod_menu_version}"

	modCompileOnly ("com.github.svenhjol:Charm:2.3.2") {
		transitive = false
	}
	modCompileOnly "com.moandjiezana.toml:toml4j:0.7.2"
	modCompileOnly "com.electronwill.night-config:core:3.6.2"
	modCompileOnly "com.electronwill.night-config:toml:3.6.2"

	modCompileOnly "curse.maven:potion_of_bees-356673:3052244"
	modCompileOnly "de.siphalor:nbtcrafting-1.16:2+"

	modCompileOnly "io.github.cottonmc:LibCD:${project.libcd_version}"
	modCompileOnly "com.github.Draylar:bee-better:${project.beebetter_version}"

	modImplementation "com.telepathicgrunt:Blame-Fabric:1.16.5-3.2.0-fabric"
}

apply from: 'gradle/processresources.gradle'

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

apply from: 'gradle/maven.gradle'
apply from: 'gradle/curseforge.gradle'
apply from: 'gradle/modrinth.gradle'
