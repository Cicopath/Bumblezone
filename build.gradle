plugins {
	id 'fabric-loom' version '0.4-SNAPSHOT'
	id 'maven-publish'
}


sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

minecraft {
	accessWidener = file("src/main/resources/the_bumblezone.accesswidener")
}

repositories {
	maven {
		name = "Ladysnake Libs"
		url = 'https://dl.bintray.com/ladysnake/libs'
	}
	maven {
		url 'https://maven.fabricmc.net/io/github/prospector/modmenu/'
	}
	maven {
		name "cotton-config"
		url 'http://server.bbkr.space:8081/artifactory/libs-release'
	}
	//flatDir {
	//	dirs 'deps'
	//}
}

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation ("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}") {
		force = true //Force the version we want rather than trusting Gradle to pick it over transitive suggestions
	}

	include "io.github.cottonmc:Jankson-Fabric:${project.jankson_version}"
	include modImplementation ("io.github.cottonmc.cotton:cotton-config:${project.cotton_config_version}")
	include "io.github.cottonmc.cotton:cotton-logging:${project.cotton_logging_version}"

	include modApi ("io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:${project.cardinal_version}")
	include modApi ("io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:${project.cardinal_version}")

	//modRuntime "com.github.commoble:potionofbees:1.0.1"

	//modImplementation ("io.github.prospector:modmenu:${project.mod_menu_version}")
	//modImplementation ("io.github.alloffabric:BeeProductive:${project.beeproductive_version}")
}

import groovy.json.JsonSlurper
import groovy.json.JsonOutput

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}

	// Minifies all .json files when building the mod.
	// Source files are not minified, only the jar copies.
	doLast {

		def jsonMinifyStart = System.currentTimeMillis()
		def jsonMinified = 0
		def jsonBytesSaved = 0

		fileTree(dir: outputs.files.asPath, include: '**/*.json').each {

			File file = it
			jsonMinified++
			def oldLength = file.length()
			file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
			jsonBytesSaved += oldLength - file.length()
		}

		println('Minified ' + jsonMinified + ' json files. Saved ' + jsonBytesSaved + ' bytes. Took ' + (System.currentTimeMillis() - jsonMinifyStart) + 'ms.')
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}
