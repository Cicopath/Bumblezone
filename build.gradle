buildscript {
	repositories {
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath group: 'gradle.plugin.com.modrinth.minotaur', name: 'Minotaur', version: '1.2.1'
	}
}

plugins {
	alias(libs.plugins.quilt.loom)
	id 'maven-publish'
	id "net.darkhax.curseforgegradle" version "1.0.7"
}

archivesBaseName = "${project.modid}_quilt"
version = "${project.mod_version}+${libs.versions.minecraft.get()}"
group = project.maven_group

loom {
	accessWidenerPath = file("src/main/resources/the_bumblezone.accesswidener")
}

configurations.all {
	resolutionStrategy.dependencySubstitution {
		substitute module('net.fabricmc:fabric-loader') using module(libs.quilt.loader.get().toString())
		all { DependencySubstitution dependency ->
			if (dependency.requested instanceof ModuleComponentSelector && dependency.requested.group == 'net.fabricmc.fabric-api') {
				if (dependency.requested.module == 'fabric-api')
					dependency.useTarget libs.quilted.fabric.api.get().toString()
				else
					dependency.useTarget 'org.quiltmc.quilted-fabric-api' + ':' + dependency.requested.module + ':' + libs.versions.quilted.fabric.api.get()
			}
		}
	}
}

repositories {
	maven { url "https://api.modrinth.com/maven/" }
	maven { url "https://www.cursemaven.com" }
	maven { url "https://maven.shedaniel.me/" }
	maven {
		name = "Ladysnake Libs"
		url = "https://ladysnake.jfrog.io/artifactory/mods"
	}
	maven {
		url "https://nexus.resourcefulbees.com/repository/maven-public/"
	}
	maven {
		url = 'https://maven.cafeteria.dev'
		content {
			includeGroup 'dev.cafeteria'
		}
	}
	repositories {
		maven { url "https://maven.shedaniel.me" }
	}
}

dependencies {
	minecraft libs.minecraft
	mappings( loom.layered {
		addLayer quiltMappings.mappings("org.quiltmc:quilt-mappings:${libs.versions.quilt.mappings.get()}:v2")
		officialMojangMappings()
	})
	modImplementation libs.quilt.loader
	modImplementation libs.quilted.fabric.api

	modApi"me.shedaniel.cloth:cloth-config-fabric:${libs.versions.cloth.config.get()}"

	modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${libs.versions.cardinal.get()}"
	modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${libs.versions.cardinal.get()}"
	include "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${libs.versions.cardinal.get()}"
	include "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${libs.versions.cardinal.get()}"

	// https://github.com/CafeteriaGuild/fake-player-api
	modImplementation "dev.cafeteria:fake-player-api:${libs.versions.fake.player.get()}"
	include "dev.cafeteria:fake-player-api:${libs.versions.fake.player.get()}"

	modImplementation "maven.modrinth:modmenu:${libs.versions.mod.menu.get()}"

	modRuntimeOnly "com.telepathicgrunt:FeatureNBTDeadlockBeGone-Quilt:${libs.versions.nbtdbg.get()}"

	modCompileOnly "me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:${libs.versions.rei.get()}"
	modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:${libs.versions.rei.get()}"
	modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:${libs.versions.rei.get()}"

	modRuntimeOnly "curse.maven:shutup_experimental_settings-401978:3677546"
	modCompileOnly "curse.maven:friends_and_foes-551364:3725313"
	modCompileOnly "curse.maven:bee_better-351600:3726160"
	//modRuntimeOnly "curse.maven:architectury-419697:3717905"
	//modRuntimeOnly "curse.maven:bee_better-351600:3726160"

	//modRuntimeOnly "curse.maven:requiem-265729:3639233"
	//modImplementation ("curse.maven:mobcatcher-498389:3575752") { transitive = false }
	//modCompileOnly ("curse.maven:charm-318872:3355096") { transitive = false }
}

apply from: 'gradle/processresources.gradle'

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = 'UTF-8'
	it.options.release.set(17)
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${archivesBaseName}"}
	}
}

apply from: 'gradle/maven.gradle'
apply from: 'gradle/modrinth.gradle'
task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
	apiToken = "$System.env.CURSEFORGEKEY"
	int projectId = 363949
	String changelogText = new File("CHANGELOG.md").getText('UTF-8')

	def mainFile = upload(projectId, remapJar)
	mainFile.changelog = changelogText
	mainFile.changelogType = "markdown"
	mainFile.releaseType = "release"
	mainFile.displayName = "The Bumblezone v${project.mod_version} (${libs.versions.minecraft.get()} MC Quilt)"
	mainFile.addRequirement("cloth-config")
	mainFile.addRequirement("fabric-api")
	mainFile.addRequirement("feature-nbt-deadlock-be-gone")
}

task buildAndRelease{}
tasks.publish.mustRunAfter tasks.build
tasks.publishCurseForge.mustRunAfter tasks.publish
tasks.publishModrinth.mustRunAfter tasks.publish
tasks.publishCurseForge.dependsOn("remapJar")
tasks.buildAndRelease.dependsOn(tasks.build, tasks.publish, tasks.publishCurseForge, tasks.publishModrinth)
