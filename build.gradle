plugins {
	id 'fabric-loom' version '0.2.7-SNAPSHOT'
	id 'maven-publish'
}


sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

minecraft {
	accessWidener = file("src/main/resources/the_bumblezone.accesswidener")
}

repositories {
	maven {
		name = "Onyx Studios"
		url = "https://maven.onyxstudios.dev"
	}
	maven {
		url 'https://maven.fabricmc.net/io/github/prospector/modmenu/'
	}
	maven {
		url 'http://server.bbkr.space:8081/artifactory/libs-release'
	}
	maven {
		url = "https://jitpack.io"
	}
}

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation ("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}") {
		force = true //Force the version we want rather than trusting Gradle to pick it over transitive suggestions
	}

	modImplementation ("io.github.cottonmc.cotton:cotton:${project.cotton_version}") {
		exclude(group: "io.github.prospector.modmenu", module: "ModMenu")
		exclude(group: "io.github.cottonmc", module: "Jankson")
	}

	include modApi ("com.github.NerdHubMC.Cardinal-Components-API:cardinal-components-base:${project.cardinal_version}")
	include modApi ("com.github.NerdHubMC.Cardinal-Components-API:cardinal-components-entity:${project.cardinal_version}")

	//modImplementation ("io.github.prospector:modmenu:${project.mod_menu_version}")
	//modImplementation ("io.github.alloffabric:BeeProductive:${project.beeproductive_version}")
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// select the repositories you want to publish to
	repositories {
		// uncomment to publish to the local maven
		// mavenLocal()
	}
}
